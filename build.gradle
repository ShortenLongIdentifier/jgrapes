import java.util.regex.Pattern
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import aQute.bnd.osgi.Processor

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.2.0'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {

    apply plugin: 'biz.aQute.bnd.builder'

    // The builder plugin doesn't provide access to the bnd properties.
    // Provide workaround as properties in extra bnd
    new Processor().withCloseable { processor ->
        processor.setProperties(project.file('bnd.bnd'))
        ext.set("bnd", processor.getFlattenedProperties());
    }

    group = 'jgrapes'
    version = bnd.base_version + "-SNAPSHOT"

    dependencies {
        compileOnly 'org.osgi:org.osgi.annotation:6.0.0'
        testCompile 'junit:junit:4.12'
    }

    // Configure sensible layout
    sourceSets {
        main {
            java {
                srcDir 'src'
            }
            resources {
                srcDir 'resources'
            }
        }

        test {
            java {
                srcDir 'test'
            }
            resources {
                srcDir 'resources'
            }
        }
    }
    
    test {
        testLogging {
            events TestLogEvent.FAILED,
                   TestLogEvent.SKIPPED,
                   TestLogEvent.PASSED,
                   TestLogEvent.STANDARD_ERROR,
                   TestLogEvent.STANDARD_OUT

            exceptionFormat TestExceptionFormat.FULL

            showStandardStreams = true
            showExceptions = true
            showStackTraces = true
            showCauses = true
        }
    }
}

task javadoc(type: Javadoc) {
    group 'documentation'
    
    def javaVersionMatcher = 
        org.gradle.internal.jvm.Jvm.current() =~ 
        /(\d+)\.(\d+)\.(\d+)_(\d+) .*/
    if (javaVersionMatcher.matches()) {
        def major = javaVersionMatcher.group(1).toInteger()
        def minor = javaVersionMatcher.group(2).toInteger()
        def patch = javaVersionMatcher.group(3).toInteger()
        def build = javaVersionMatcher.group(4).toInteger()
        if (major > 1
            || major == 1 && minor > 8
            || major == 1 && minor == 8 && patch > 0
            || major == 1 && minor == 8 && patch == 0 && build >= 121) {
            options.addBooleanOption("-allow-script-in-comments", true)
        }
    }

    classpath = files(subprojects.collect {project -> 
        project.sourceSets.main.compileClasspath})
    source subprojects.collect {project -> project.sourceSets.main.allJava } 
    destinationDir = file("../jgrapes.gh-pages/javadoc")
    options.addStringOption("bottom", file("misc/javadoc.bottom.txt").text)
    options.addStringOption("link", "http://docs.oracle.com/javase/8/docs/api/")
    
    options.addStringOption("tag", "org.osgi.annotation.versioning.Version:x")
}
