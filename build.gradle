import aQute.bnd.osgi.Processor

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:6.1.0'
    }
}

plugins {
	id "maven-publish"
	id "signing"
	id "com.moowork.node" version "1.2.0"
    id 'org.ajoberstar.grgit' version '4.1.0'
    id 'org.ajoberstar.git-publish' version '3.0.0'
}

ext {
	isCiBuild = System.getenv().get("CI") == 'true'
    isJitPackBuild = System.getenv().get("JITPACK") == 'true'
}

// Prepare github authentication for plugins
if (System.properties['org.ajoberstar.grgit.auth.username'] == null) {
	System.setProperty('org.ajoberstar.grgit.auth.username',
		System.getenv("GH_TOKEN") ?: project.properties['github.token'] ?: "nouser")
}

allprojects {
    repositories {
        mavenCentral()
        // Sometimes, synchronization with central is too slow
        maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
        // Snapshots
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        // Experimental, not sure if this is the way tpo go...
        maven { url 'https://jitpack.io' }
    }
    
    group = 'org.jgrapes'
    
    ext.releaseVersion = "1.3.2"
    ext.isSnapshot = false

    // Default values, can be overridden in subproject
    version = "${-> releaseVersion + (isSnapshot ? '-SNAPSHOT' : '') }"
    
    apply from: "${project.rootDir}/gradle/eclipse.gradle"
}

subprojects {
    apply from: "${project.rootDir}/gradle/subprojects.gradle"
}

gitPublish {
	repoUri = 'https://github.com/mnlipp/jgrapes.git'
	branch = 'gh-pages'
	contents {
		from("${buildDir}/javadoc") {
			into 'javadoc'
		}
		if (!isSnapshot) {
			from("${buildDir}/javadoc") {
				into 'latest-release/javadoc'
			}
		}
	}
	preserve { include '**/*' }
	commitMessage = "Updated."
}

// Until https://github.com/ajoberstar/gradle-git-publish/issues/41 is fixed
tasks.gitPublishCopy.dependsOn(":org.jgrapes.core:java11doc")

task stage {
	description = 'To be executed by CI, build and update JavaDoc.'
	group = 'build'

    // Build everything first
    subprojects.tasks.each { tc -> dependsOn tc.findByName("build") }
	
    if (!isCiBuild || JavaVersion.current() == JavaVersion.VERSION_15) {
    	// Publish JavaDoc
    	dependsOn gitPublishPush
    }
}

