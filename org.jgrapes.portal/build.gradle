buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.github.ksoichiro:gradle-web-resource-plugin:1.7.3'
	}
}

apply plugin: 'com.github.ksoichiro.web.resource'

repositories {
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

webResource {
	base {
		// Change base directories for src/dest
		src = 'webResource'
		dest = 'build/generated/webResources/org/jgrapes/portal'
	}
	
	coffeeScript {
        // Default: true
        minify = !project.version.endsWith("-SNAPSHOT")
    }
}

classes.dependsOn 'webResourceCompile'
sourceSets.main.output.dir project.file('build/generated/webResources')


dependencies {
	compile project(':org.jgrapes.util')
	compile project(':org.jgrapes.http')
	compile 'org.freemarker:freemarker:2.3.26-incubating'
	compile group: 'org.glassfish', name: 'javax.json', version: '1.1'
	compile 'org.jdrupes.json:json:1.0.+'
}

jar {
    into('META-INF') {
        from 'resources/META-INF'
    }
}

apply plugin: 'com.moowork.node'

ext.jsDocDestDir = "${rootProject.projectDir}/../jgrapes.gh-pages/portal/jsdoc" 

task documentationjs(type: NodeTask) {
	dependsOn(':npmInstall')
	new File(jsDocDestDir).mkdirs()
	script = file("${rootProject.projectDir}/node_modules/documentation/bin/documentation.js")
	args = ["build", "--format", "html", "--output", jsDocDestDir,
		"resources/org/jgrapes/portal/functions.js"]
}